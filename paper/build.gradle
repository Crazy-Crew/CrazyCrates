import io.papermc.hangarpublishplugin.model.Platforms

plugins {
    alias(libs.plugins.paperweight)
    alias(libs.plugins.shadowjar)

    alias(libs.plugins.modrinth)

    alias(libs.plugins.runpaper)

    alias(libs.plugins.hangar)
}

base {
    archivesName = "${rootProject.name}"
}

dependencies {
    api(project(":common"))

    implementation(libs.cluster.paper)

    implementation(libs.triumph.cmds)

    implementation(libs.metrics)

    implementation(libs.nbtapi)

    compileOnly(libs.holographicdisplays)

    compileOnly(libs.decentholograms)

    compileOnly(libs.placeholderapi)

    compileOnly(libs.itemsadder)

    compileOnly(libs.oraxen)

    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])

    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:$minecraftVersion-R0.1-SNAPSHOT")
}

String type = isBeta ? "Beta" : "Release"

String description = """
## Changes:
 * Nested placeholders now work *with limitations
  * %crazycrates_<player>_<crate>_opened% must be done like %crazycrates_{player_name}_your_crate_opened%
 * {player_name} can be replaced with almost any player variable from https://github.com/PlaceholderAPI/PlaceholderAPI/wiki/Placeholders

## Other:
 * [Feature Requests](https://github.com/Crazy-Crew/${rootProject.name}/issues)
 * [Bug Reports](https://github.com/Crazy-Crew/${rootProject.name}/issues)
"""

File file = project.layout.buildDirectory.file("libs/${rootProject.name}-${rootProject.version}.jar").get().asFile

tasks {
    // Publish to hangar.papermc.io.
    hangarPublish {
        publications.register("plugin") {
            version.set("$rootProject.version")

            id.set(rootProject.name)

            channel.set(type)

            changelog.set(description)

            apiKey.set(System.getenv("hangar_key"))

            platforms {
                register(Platforms.PAPER) {
                    jar.set(file)

                    platformVersions.set(["$minecraftVersion"])
                }
            }
        }
    }

    // Publish to modrinth.
    modrinth {
        setAutoAddDependsOn(false)

        token.set(System.getenv("modrinth_token"))

        projectId.set(rootProject.name.toLowerCase())

        versionName.set("${rootProject.name} ${rootProject.version}")

        versionNumber.set("${rootProject.version}")

        versionType.set(type.toLowerCase())

        uploadFile.set(file)

        gameVersions.add(minecraftVersion)

        changelog.set(description)

        loaders.addAll("paper", "purpur")
    }

    // Runs a test server.
    runServer {
        jvmArgs("-Dnet.kyori.ansi.colorLevel=truecolor")

        minecraftVersion(minecraftVersion)
    }

    // Assembles the plugin.
    assemble {
        dependsOn(reobfJar)
    }

    shadowJar {
        // Remove classifier
        archiveClassifier.set("")

        // Exclude meta inf.
        exclude("META-INF/**")

        // Relocate dependencies.
        List.of(
                "com.ryderbelserion.cluster.paper",
                "org.jetbrains.annotations",
                "de.tr7zw.changeme.nbtapi",
                "dev.triumphteam.cmd",
                "org.bstats"
        ).forEach {
            relocate(it, "com.badbones69.crazycrates.libs.$it")
        }
    }

    // Updates variables in the plugin.yml
    processResources {
        Map<String, String> props = new HashMap<>()

        props.put("name", rootProject.name)
        props.put("version", "$rootProject.version")
        props.put("group", "$project.group")
        props.put("description", rootProject.description)

        props.put("apiVersion", apiVersion)
        props.put("authors", authors)
        props.put("website", website)

        inputs.properties(props)

        filesMatching("plugin.yml") {
            expand(props)
        }
    }
}